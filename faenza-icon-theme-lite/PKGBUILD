## Contributor: Daniel Micay <danielmicay@gmail.com>
## Contributor: Elia Notarangelo <elia.notarangelo AT gmail DOT com>
## Contributor: Frikilinux <frikilinux at frikilinux.com.ar>
## Contributor: graysky <graysky AT archlinux DOT us>
## Contributor: Samsagax <samsagax AT gmail DOT com>
## Contributor: simongmzlj <simongmzlj AT gmail DOT com>
pkgname=faenza-icon-theme-lite
pkgver=1.3.1
pkgrel=3
pkgdesc='Icon theme designed for Equinox GTK theme'
url='http://gnome-look.org/content/show.php/Faenza?content=128143'
license=('GPL3')
conflicts=("${pkgname%-*}")
arch=('any')
options=(!strip)
source=('http://ppa.launchpad.net/tiheum/equinox/ubuntu/pool/main/f/faenza-icon-theme/faenza-icon-theme_1.3.1.tar.gz')
md5sums=('b5339b70cbb821b583499e725957b150')

package() {
	cd "${pkgname%-*}-1.3"
	install -dm755 "${pkgdir}/usr/share/icons/Faenza"

	cp Faenza/index.theme "${pkgdir}/usr/share/icons/Faenza"
	for i in actions apps categories mimetypes emblems devices places stock stock/io status;do
		mkdir "${pkgdir}/usr/share/icons/Faenza/${i}"
		cp -rf Faenza/${i}/scalable "${pkgdir}/usr/share/icons/Faenza/${i}"
	done
	for i in actions apps categories mimetypes emblems devices places stock stock/io status;do
		cp -rf Faenza/${i}/{16,22,24} "${pkgdir}/usr/share/icons/Faenza/${i}"
	done

	## set the Arch Linux Distributor and start menu logo
	cd "${pkgdir}/usr/share/icons/Faenza/places/scalable/"
	ln -sf distributor-logo{-archlinux,}.svg
	ln -sf start-here{-archlinux,}.svg
	ln -sf start-here{-archlinux-symbolic,-symbolic}.svg
}
