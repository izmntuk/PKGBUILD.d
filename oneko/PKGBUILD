## Contributor: mathieui <mathieui[at]mathieui.net>
pkgname=oneko
pkgrel=4
pkgver=1.2.5
_pkgver="1.2.sakura.5"
pkgdesc="A cat that chases around your cursor"
arch=('x86_64' 'i686')
url="http://www.daidouji.com/oneko/"
license=('custom')
depends=('libx11' 'libxext')
makedepends=('glibc' 'imake' 'make' 'desktop-file-utils')
source=("http://www.daidouji.com/${pkgname}/distfiles/${pkgname}-${_pkgver}.tar.gz")
sha1sums=('f48f41080e8acb76a7d30367f35afaeeb729721a')

prepare() {
	cd "${pkgname}-${_pkgver}"
	iconv -f ISO-2022-JP -t UTF-8 README > README.UTF-8
	iconv -f ISO-2022-JP -t UTF-8 README-SUPP > README-SUPP.UTF-8
}

build() {
	cd "${pkgname}-${_pkgver}"
	xmkmf -a && make
}

package() {
	cd "${pkgname}-${_pkgver}"
	install -d "${pkgdir}/usr/share/doc/${pkgname}"
	install -d "${pkgdir}/usr/share/man/man1"
	install -d "${pkgdir}/usr/share/man/jp/man1"
	install -d "${pkgdir}/usr/bin"
	install -Dm444 README.UTF-8 "${pkgdir}/usr/share/doc/${pkgname}/README"
	install -Dm444 README-SUPP.UTF-8 "${pkgdir}/usr/share/doc/${pkgname}/README-SUPP"
	install -pm444 README-NEW "${pkgdir}/usr/share/doc/${pkgname}"
	install -m755 oneko "${pkgdir}/usr/bin"
	install -Dm444 oneko.man "${pkgdir}/usr/share/man/man1/oneko.1"
	install -Dm444 oneko.man.jp "${pkgdir}/usr/share/man/jp/man1/oneko.1"
}
